# vim:syntax=cf3
#
# TODO ensure permissions

bundle agent c12_base_services
{
    vars:
        fedora_19::
            "base_services[postfix][id]"      string => "postfix";
            "base_services[postfix][package]" string => "postfix";
            "base_services[postfix][conf]"    string => "/etc/postfix/main.cf";

            "base_services[ntp][id]"      string => "ntpd";
            "base_services[ntp][package]" string => "ntp";
            "base_services[ntp][conf]"    string => "/etc/ntp.conf";

            "base_services[ssh][id]"      string => "sshd";
            "base_services[ssh][package]" string => "openssh-server";
            "base_services[ssh][conf]"    string => "/etc/ssh/sshd_config";

    methods:
        "configure_base_services" usebundle => configure_base_services("$(this.bundle).base_services");
}

bundle agent configure_base_services(srv)
{
    vars:
        "service" slist => getindices("$(srv)");

    files:
        "$($(srv)[$(service)][conf])"
               depends_on => { "files_setup" },
            edit_defaults => empty,
                  changes => diff,
            edit_template => "$(global_vars.templates_dir)/$(sys.flavor)/$($(srv)[$(service)][conf])",
                  classes => if_repaired("base_services_fixed_$(service)");

    packages:
        "$($(srv)[$(service)][package])"
                   comment => "Ensure $(service) is installed",
                    handle => "c11_setup_$(service)_package",
            package_policy => "add",
            package_method => yum_rpm;

    services:
        "$($(srv)[$(service)][id])"
               comment => "Restart a service if we've messed with the config file",
            depends_on => { "c11_setup_$(service)_package", "base_services_starting_$(service)" },
        service_policy => "restart",
        service_method => custom_service,
            ifvarclass => "base_services_fixed_$(service)";

    reports:
        verbose_mode::
            "Making sure common services are running and configured";

        inform_mode::
            "$(service): Package=$($(srv)[$(service)][package])";
}
