# vim:syntax=cf3:smarttab:expandtab:ts=2:sts=2:sw=2
#

bundle agent b04_ldap_setup
{
  vars:
    redhat::
      "cacertdir" string => "/etc/openldap/cacerts";

  methods:
    any::
      "setup_ldap_cacerts" usebundle => setup_ldap_cacerts;
}

bundle agent setup_ldap_cacerts
{
  vars:
    any::
      "bburke_cafile" string => "$(b04_ldap_setup.cacertdir)/bburke_v$(global_vars.ca_ver).crt";

    bburke_ca_present::
      "bburke_hash" string => execresult("$(global_vars.openssl) x509 -in $(bburke_cafile) -noout -subject_hash", "noshell");

  classes:
    any::
      "bburke_ca_present" expression => fileexists("$(bburke_cafile)");

  files:
    any::
      "$(bburke_cafile)"
        comment       => "Bring in any new CA cert files",
        handle        => "b04_setup_ldap_cacerts_bburke",
        create        => "true",
        copy_from     => local_dcp("$(global_vars.generic_dir)/bburke.crt"),
        edit_defaults => empty,
        changes       => detect_all_change,
        perms         => mog("0644", "root", "root"),
        classes       => if_repaired("bburke_ca_file_added");

      "$(b04_ldap_setup.cacertdir)/$(bburke_hash).0"
        comment    => "Create LDAP cacert hash file/link",
        handle     => "b04_setup_ldap_cacerts_bburke_hash",
        link_from  => ln_s("$(bburke_cafile)"),
        ifvarclass => "bburke_ca_present";

  commands:
    redhat::
      "$(global_vars.restorecon) -R $(b04_ldap_setup.cacertdir)"
        ifvarclass => "bburke_ca_file_added";
}
