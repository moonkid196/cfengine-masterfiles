# vim:syntax=cf3:smarttab:expandtab:ts=2:sts=2:sw=2
#

bundle agent d40_base_services
{
  vars:
    # All SSH, all the time
    servers::
      "ssh_conf[PasswordAuthentication]" string => "no";
      "ssh_conf[AllowGroups]"            string => "root wheel admins",
                                         policy => "overridable";
    !servers::
      "ssh_conf[PasswordAuthentication]" string => "yes";
      "ssh_conf[PermitRootLogin]"        string => "without-password";
      "ssh_conf[AllowGroups]"            string => "root wheel sshusers";
    web_servers::
      "ssh_conf[AllowGroups]"            string => "root wheel webadmins";

    # We have a postfix
    !mail_servers::
      "postfix_conf[smtp_tls_CAfile]"              string => "/etc/pki/tls/certs/bburke.crt";
      "postfix_conf[smtp_tls_mandatory_ciphers]"   string => "HIGH";
      "postfix_conf[smtp_tls_mandatory_protocols]" string => "!SSLv3, !SSLv2";
      "postfix_conf[smtp_tls_security_level]"      string => "secure";
      "postfix_conf[relayhost]"                    string => "[smtp.baburke.net]";
      "postfix_conf[mydestination]"                string => "";

    # We are the time-keepers
    !ntp_servers.internal::
      "ntp_conf[servers]" string => "ntp.baburke.net";

  methods:
    any::
      "ssh"     usebundle => ssh_setup("$(this.bundle).ssh_conf");
      "postfix" usebundle => postfix_setup("$(this.bundle).postfix_conf");
      "ntp"     usebundle => ntp_setup("$(this.bundle).ntp_conf");
}

bundle agent ssh_setup(conf)
{
  vars:
    fedora_19::
      "file" string => "/etc/ssh/sshd_config";
      "pkg"  string => "openssh-server";
      "srv"  string => "sshd";

  files:
    any::
      "$(file)"
        comment   => "Edit sshd configuration",
        handle    => "base_services_ssh_files_sshd",
        changes   => detect_content,
        perms     => system_owned("0600"),
        edit_line => set_line_based("$(conf)", " ", "\s+", ".*", "\s*#\s*"),
        classes   => if_repaired("ssh_reload");

  packages:
    any::
      "$(pkg)"
        package_policy => "verify",
        classes        => if_notkept("needs_ssh");

  methods:
    any::
      "ssh"
        usebundle  => install_packages("$(pkg)"),
        ifvarclass => "needs_ssh";

  services:
    any::
      "$(srv)"
        service_policy => "start",
        ifvarclass     => "!needs_ssh";

      "$(srv)"
        service_policy => "reload",
        ifvarclass     => "ssh_reload";
}

bundle agent postfix_setup(conf)
{
  vars:
    fedora_19::
      "file" string => "/etc/postfix/main.cf";
      "pkg"  string => "postfix";
      "srv"  string => "postfix";

  files:
    any::
      "$(file)"
        comment   => "Edit postfix configuration",
        handle    => "base_services_postfix_files_main",
        changes   => detect_content,
        perms     => system_owned("0644"),
        edit_line => set_line_based("$(conf)", "=", "\s*=\s*", ".*", "\s*#\s*"),
        classes   => if_repaired("postfix_reload");

  packages:
    any::
      "$(pkg)"
        package_policy => "verify",
        classes        => if_notkept("needs_postfix");

  methods:
    any::
      "postfix"
        usebundle  => install_packages("$(pkg)"),
        ifvarclass => "needs_postfix";

  services:
    any::
      "$(srv)"
        service_policy => "start",
        ifvarclass     => "!needs_postfix";

      "$(srv)"
        service_policy => "reload",
        ifvarclass     => "postfix_reload";
}

bundle agent ntp_setup(conf)
{
  vars:
    fedora_19::
      "file" string => "/etc/ntp.conf";
      "pkg"  string => "ntp";
      "srv"  string => "ntpd";

  files:
    any::
      "$(file)"
        comment   => "Remove extraneous server lines",
        handle    => "base_services_ntp_files_delete",
        changes   => detect_content,
        perms     => system_owned("0644"),
        edit_line => insert_if_no_line_matching("\s*server\s+$(conf[server])\s*", "server $(conf[server])", "true"),
        classes   => if_repaired("ntp_reload");

      "$(file)"
        comment   => "Edit ntpd configuration",
        handle    => "base_services_ntp_files_conf",
        changes   => detect_content,
        perms     => system_owned("0644"),
        edit_line => set_line_based("$(conf)", " ", "\s+", "(?!server).*", "\s*#\s*"),
        classes   => if_repaired("ntp_reload");

  packages:
    any::
      "$(pkg)"
        package_policy => "verify",
        classes        => if_notkept("needs_ntp");

  methods:
    any::
      "ntp"
        usebundle  => install_packages("$(pkg)"),
        ifvarclass => "needs_ntp";

  services:
    any::
      "$(srv)"
        service_policy => "start",
        ifvarclass     => "!needs_ntp";

      "$(srv)"
        service_policy => "reload",
        ifvarclass     => "ntp_reload";
}
