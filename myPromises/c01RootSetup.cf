# vim:syntax=cf3

bundle agent c01_root_setup
{
    vars:
        any::
            "root_home_dir"     string => "/root";
            "inputs_dir"        string => "/var/cfengine/inputs";

            "templates_dir"     string => "$(inputs_dir)/myTemplates";
            "root_templates"    string => "$(templates_dir)/root";

            "passwd_shell_field"   int => "7";
            "root_passwd"       string => execresult("$(getent) root passwd", "useshell");
            "root_passwd_array"  slist => splitstring("$(root_passwd)", ":", "7");

        fedora_19::
            "getent" string => "/usr/bin/getent";
            "zsh"    string => "/bin/zsh";

    classes:
        any::
            "root_has_zsh" expression => strcmp("$(zsh)", "$(root_passwd_array)[$(passwd_shell_field)]");

    files:
        any::
            "$(root_home_dir)"
                comment         => "Make root's home area nicer",
                handle          => "root_setup_home_dir",
                changes         => detect_all_change,
                copy_from       => backup_local_cp("$(root_templates)"),
                depth_search    => recurse_with_base("inf"),
                perms           => mog("0600", "root", "root"),
                create          => "true",
                classes         => if_repaired("relabel_root_home");

        any.!root_has_zsh::
            "/etc/passwd"
                depends_on => { "base_packages_install" },
                   comment => "Set root shell to $(zsh)",
                    handle => "root_setup_add_zsh",
                   classes => if_repaired("root_setup_added_zsh"),
                   changes => detect_all_change,
                 edit_line => set_user_field("root", "$(passwd_shell_field)", "$(zsh)");

    commands:
        # Should probably just check for selinux being on
        fedora_19.relabel_root_home::
            "/usr/sbin/restorecon -R $(root_home_dir)";

    reports:
        root_setup_added_zsh::
            "Setting root's shell to $(zsh)";
}
